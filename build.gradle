group 'com.r3cev.prototyping'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
//apply plugin: 'org.jetbrains.dokka'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

buildscript {
    ext.kotlin_version = '1.0.0-beta-4584'
    ext.quasar_version = '0.7.4-SNAPSHOT'
    ext.asm_version = '0.5.3'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.4"
    }
}


repositories {
    // mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    jcenter()
}

configurations {
    quasar
}

// This block makes Gradle print an error and refuse to continue if we get multiple versions of the same library
// included simultaneously.
configurations.all() {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile "com.google.guava:guava:19.0"
    compile("com.esotericsoftware:kryo:3.0.3") {
        force = true
    }
    compile "de.javakaffee:kryo-serializers:0.37"
    compile "com.google.code.findbugs:jsr305:3.0.1"

    // Logging
    compile "org.slf4j:slf4j-jdk14:1.7.13"

    // Quasar: for the bytecode rewriting for state machines.
    compile("co.paralleluniverse:quasar-core:${quasar_version}:jdk8") {
        // Quasar currently depends on an old version of Kryo, but it works fine with the newer version, so exclude it
        // here so the newer version is picked up.
        exclude group: "com.esotericsoftware.kryo", module: "kryo"
    }
    quasar("co.paralleluniverse:quasar-core:${quasar_version}:jdk8@jar") {
        exclude group: "com.esotericsoftware.kryo", module: "kryo"
    }

    // For visualisation
    compile "org.graphstream:gs-core:1.3"
    compile "org.graphstream:gs-ui:1.3"
    compile("com.intellij:forms_rt:7.0.3") {
        exclude group: "asm"
    }
}

// These lines tell Gradle to add a couple of JVM command line arguments to unit test and program runs, which set up
// the Quasar bytecode rewriting system so fibers can be suspended. The verifyInstrumentation line makes things run
// slower but you get a much better error message if you forget to annotate a method with @Suspendable that needs it.
//
// In Java 9 (hopefully) the requirement to annotate methods as @Suspendable will go away.
tasks.withType(Test) {
    jvmArgs "-javaagent:${configurations.quasar.singleFile}"
    jvmArgs "-Dco.paralleluniverse.fibers.verifyInstrumentation"
}
tasks.withType(JavaExec) {
    jvmArgs "-javaagent:${configurations.quasar.singleFile}"
    jvmArgs "-Dco.paralleluniverse.fibers.verifyInstrumentation"
}
